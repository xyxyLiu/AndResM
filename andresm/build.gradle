apply plugin: 'groovy'
apply plugin: "maven"
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.auto.value:auto-value:1.0'
    compile 'com.android.tools.build:gradle:2.0.0'
}

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

version = "0.2.0"
group = "com.reginald"

def siteUrl = 'https://github.com/xyxyLiu/AndResM'    // project homepage
def gitUrl = 'https://github.com/xyxyLiu/AndResM.git' // project git

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                name 'AndResM'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'xyxyLiu'
                        name 'xyxyLiu'
                        email 'tonyreginald@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

def I = File.separator

javadoc {
    failOnError = false
}

task sourcesJar(type: Jar) {
    from new File(project.getProjectDir(), "src${I}main${I}groovy")
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "andresm"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = '0.2.0'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../repo'))
        }
    }
}
